apply plugin: 'maven'

curseforge {
    if (project.hasProperty("CURSEFORGE_API")) {
        apiKey = project.CURSEFORGE_API
    } else if (System.getenv().CI) {
        apiKey = System.getenv().CURSEFORGE_KEY_SECRET
    } else {
        apiKey = "DUMMY"
    }
    project {
        id = "233029"

        if (config.type == "beta") {
            releaseType = 'beta'
            //changelog = new File("resources/changelog/${project.version}.txt").text
        } else if (config.type == "release") {
            releaseType = 'release'
        } else {
            releaseType = 'alpha'
            changelog = "This version should not be used in your 'productive' world. It contains the latest features, but also the latest bugs.\n Probably not all features are finished.\n To see what might have changed visit our Github page and browse the latest commits."
        }
        addGameVersion config.minecraft_version

        mainArtifact(jar) {
            displayName = "Vampirism-${config.minecraft_version}-${project.mod_version}"


        }


    }


}

configurations {
    deployerJars
}

dependencies {
    deployerJars "org.apache.maven.wagon:wagon-ftp:2.9"
}

uploadArchives {
    repositories {
        add getProject().repositories.mavenLocal()
    }
    repositories.mavenDeployer {
        configuration = configurations.deployerJars

        if (project.hasProperty("filesmaven_url")) {
            logger.info('Publishing to files server')
            repository(url: project.filesmaven_url) {
                authentication(userName: project.filesmaven_username, password: project.filesmaven_key)
            }
        } else if (System.getenv().MAVEN_URL) {
            logger.info('Publishing to files server')
            repository(url: System.getenv().MAVEN_URL) {
                authentication(userName: System.getenv().MAVEN_USERNAME, password: System.getenv().MAVEN_KEY)
            }
        } else {
            logger.info('Publishing to repo folder')
            repository(url: 'file://localhost/' + project.file('~/.m2/repository').getAbsolutePath())
        }

        pom {
            groupId = project.group
            version = project.version
            artifactId = project.archivesBaseName
        }
        pom.project {
            name project.archivesBaseName
            packaging 'jar'
            description 'An Minecraft mod that allows you to become a vampire'
            url 'https://maxanier.de/projects/vampirism'

            scm {
                url 'https://github.com/TeamLapen/Vampirism'
                connection 'scm:git:git://github.com/TeamLapen/Vampirism.git'
                developerConnection 'scm:git:git@github.com:TeamLapen/Vampirism.git'
            }

            issueManagement {
                system 'github'
                url 'https://github.com/TeamLapen/Vampirism/issues'
            }

            developers {
                developer {
                    id 'maxanier'
                    name 'maxanier'
                    roles { role 'developer' }
                }
            }
        }


    }
}