repositories {
//    maven {
//        name "Mobius Repo"
//        url "http://mobiusstrip.eu/maven"
//    }
    maven { url 'http://tehnut.info/maven/' }
    maven { url 'http://chickenbones.net/maven' }
    maven {
        url "http://dvs1.progwml6.com/files/maven"
    }
    maven {
        // location of a maven mirror for JEI files, as a fallback
        name = "ModMaven"
        url = "https://modmaven.k-4u.nl"
    }
    maven {
        //Local repo for custom forge builds.
        //Is not required to exist
        name = "LocalForge"
        url = "../../MinecraftForge/repo/"
    }
//    maven {
//        name = "CurseForge"
//        url = "https://minecraft.curseforge.com/api/maven/"
//    }
    maven {
        name 'shinoow'
        url 'https://dl.bintray.com/shinoow/maven/'
    }
    mavenCentral()
}


dependencies {
    minecraft([
            group: "net.minecraftforge",
            name: "forge",
            version: "${project.minecraft_version}-${project.forge_version}"
    ])
    compileOnly fg.deobf("mezz.jei:jei-${project.minecraft_version}:${project.jei_version}:api") //API for compile
    runtimeOnly fg.deobf("mezz.jei:jei-${project.minecraft_version}:${project.jei_version}") //Full for runtime
    
    //deobfCompile 'mcp.mobius.waila:Waila:' + config.waila_version
    //deobfCompile 'Waila:Waila:1.7.1_1.11.2' //Backup Waila on curseforge
    //deobf 'info.amerifrance.guideapi:Guide-API:${guide_api_version}'


}

minecraft {
    mappings channel: "${project.mcp_channel}".toString(), version: "${project.mcp_mappings}".toString()
    accessTransformer = file('src/main/resources/META-INF/accesstransformer.cfg')

    runs {
        client {
            taskName 'runClient1'
            main 'net.minecraftforge.userdev.LaunchTesting'
            property 'forge.logging.console.level', 'debug'
            property 'forge.logging.markers', 'SCAN,REGISTRIES,REGISTRYDUMP'
            workingDirectory project.file('run/client1').canonicalPath
            jvmArg '-ea:de.teamlapen...'
            
            mods {

                vampirism {
                    source sourceSets.main
                    source sourceSets.api
                    source sourceSets.lib

                }


            }

        }
        server = {
            property 'forge.logging.markers', 'SCAN,REGISTRIES,REGISTRYDUMP'
            property 'forge.logging.console.level', 'debug'
            workingDirectory project.file('run/server').canonicalPath
            jvmArg '-ea:de.teamlapen...'

            mods {
                vampirism {
                    source sourceSets.main
                    source sourceSets.api
                    source sourceSets.lib
                }


            }
        }
    }

}

task downloadCrowdin() {
    ext {
        output = file('build/crowdin_raw.zip')
        update = file('build/crowdin.json')
        id = 'vampirism'
    }
    outputs.upToDateWhen { false }
    onlyIf {
        project.hasProperty('CROWDIN_KEY') && !project.gradle.startParameter.isOffline()
    }
    doLast {
        download {
            src "https://api.crowdin.com/api/project/${id}/export?key=${project.CROWDIN_KEY}&export_translated_only&json"
            dest update
            overwrite true
        }
        if (!update.text.contains('success')) {
            throw new RuntimeException("Crowdin export failed, see ${update} for more info")
        }
        download {
            src "https://api.crowdin.com/api/project/${id}/download/all.zip?key=${project.CROWDIN_KEY}"
            dest output
            overwrite true
        }
    }
}

task crowdin(type: Copy){
    dependsOn downloadCrowdin
    onlyIf {
        !downloadCrowdin.state.skipped
    }
    destinationDir = file('build/translations')
    from(zipTree(downloadCrowdin.output)){
        filter { String line ->
            line.indexOf("\"\"") != -1 ? null : line //Filter empty translations
        }
        filteringCharset = 'UTF-8'
        exclude { it.isDirectory() }
        rename { it.toLowerCase() }//Minecraft needs it lowercase.
        exclude '**/*.lang' //Pre-1.13 format
    }
}

processResources {

    from(sourceSets.main.resources.srcDirs) {
        include 'META-INF/mods.toml'

        expand 'version': project.mod_version, 'mcversion': minecraft_version, 'forge_version': forge_version
    }



    from(sourceSets.main.resources.srcDirs) {
        exclude 'META-INF/mods.toml'
    }
    from{
        fileTree('build/translations').matching{
            exclude "**/en_us.json" //Don't override local file
        }
    }


    exclude '**/Thumbs.db'
}

processLibResources {
    from(sourceSets.lib.resources.srcDirs) {
        include 'META-INF/mods.toml'

        expand 'version': project.mod_version, 'mcversion': minecraft_version, 'forge_version': forge_version
    }
    from(sourceSets.lib.resources.srcDirs) {
        exclude 'META-INF/mods.toml'
    }

     outputs.upToDateWhen { false }
    

}




jar {
    //Keep the jar as clean as possible
    includeEmptyDirs = false

    from sourceSets.main.output.classesDirs
    from sourceSets.api.output.classesDirs
    from sourceSets.lib.output.classesDirs
    from sourceSets.main.output.resourcesDir
    from sourceSets.api.output.resourcesDir
    from sourceSets.lib.output.resourcesDir
    
    from{
        fileTree('build/translations').matching{
            exclude "**/en_US.lang"
        }
    }

    dependsOn 'crowdin'


    manifest {
        attributes([
                "FMLAT": "accesstransformer.cfg",
                "Specification-Title": "vampirism",
                "Specification-Vendor": "maxanier",
                "Specification-Version": "1", // We are version 1 of ourselves
                "Implementation-Title": project.name,
                "Implementation-Version": "${mod_version}",
                "Implementation-Vendor" :"maxanier",
                "Implementation-Timestamp": new Date().format("yyyy-MM-dd'T'HH:mm:ssZ")
        ])
    }

}
