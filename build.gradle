buildscript {
    repositories {
        mavenCentral()
        jcenter()
        maven {
            name = "forge"
            url = "http://files.minecraftforge.net/maven"
        }
    }

    
    dependencies {
        classpath group: 'net.minecraftforge.gradle', name: 'ForgeGradle', version: '3.+', changing: true
    }
}


plugins {
    id 'com.matthewprenger.cursegradle' version '1.2.0'
    id 'de.undercouch.download' version '3.3.0'
    id 'com.wynprice.cursemaven' version '2.1.1'
}

apply plugin: 'net.minecraftforge.gradle'
apply plugin: 'idea'
apply plugin: 'eclipse'


configurations {
    optionalRuntime
    runtime.extendsFrom optionalRuntime // optionalRuntime dependencies are downloaded but not exported to POM
}

group = "de.teamlapen.vampirism"
archivesBaseName = "Vampirism"
sourceCompatibility = targetCompatibility = compileJava.sourceCompatibility = compileJava.targetCompatibility = '1.8' // Need this here so eclipse task generates correctly.
ext.mod_version="Test"
ext.type="test"


sourceSets {
    api
    lib {
        compileClasspath += configurations["minecraft"]
    }
    main {
         java{
             exclude '**/guide/*'
         }
        compileClasspath += sourceSets.lib.output
        compileClasspath += sourceSets.api.output
        runtimeClasspath += sourceSets.api.output
    }
    test {
        compileClasspath += sourceSets.api.output
        runtimeClasspath += sourceSets.api.output
    }
}


apply from: 'gradle/versioning.gradle'
apply from: 'gradle/forge.gradle'
apply from: 'gradle/dev.gradle'
apply from: 'gradle/deploy.gradle'


configurations {
    apiCompile.extendsFrom(compile)
}

idea {
    module {
        for (String excludeDirName in ["run", "out", "logs", "gradle"]) {
            File excludeDir = new File(projectDir, excludeDirName)
            excludeDirs.add(excludeDir)
        }
    }
}